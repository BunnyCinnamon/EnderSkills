buildscript {
    repositories {
        jcenter()
        maven { url = "http://files.minecraftforge.net/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "${mc_version}-${mayor}.${noapi}.${minor}.${patch}${extra}"
group = "arekkuusu.enderskills"
archivesBaseName = "EnderSkills"

compileJava.options.encoding = "UTF-8"
sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "${mc_version}-${forge_version}"
    runDir = "run"

    mappings = mc_mappings

    replace 'GRADLE:VERSION', project.version
    replaceIn 'LibMod.java'
}

repositories {
    maven {
        name = "JEI"
        url = "https://dvs1.progwml6.com/files/maven/"
    }
    flatDir {
        dirs 'lib'
    }
}

dependencies {
    compileOnly (name: "EventHelper-1.12.2-2.6.2-dev")
    shadow(name: 'com.expression.parser-2.0.0')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

project.tasks.build.dependsOn project.tasks.shadowJar

shadowJar {
    project.configurations.shadow.setTransitive(true);
    configurations = [project.configurations.shadow]

    classifier ''
}

reobf {
    shadowJar {}
}

artifacts {
    archives shadowJar
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}
